// ----------------------------------
// RSDK Project: Virtua Scarlet
// Script Description: Scarlet Engine Splash Screen (and Retro Engine's lock out screen)
// Script Author: Klashicola/Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer // Past Geared used to call this "object.whatever" when it was clearly a timer :sob:

// States
private alias 0 : SCARLETLOGO_INITIALISE
private alias 1 : SCARLETLOGO_FADEFROMBLACK
private alias 2 : SCARLETLOGO_WAIT
private alias 3 : SCARLETLOGO_FADETOBLACK
private alias 4 : SCARLETLOGO_RETROLOCKOUT

// Priority
public alias 1 : PRIORITY_ACTIVE


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case SCARLETLOGO_INITIALISE
		LoadPalette("RetroLogo.act", 0, 0, 0, 256)
		object.state = SCARLETLOGO_FADEFROMBLACK
		object.timer = 256
		SetScreenFade(0x00, 0x00, 0x00, 0xFF)
		break

	case SCARLETLOGO_FADEFROMBLACK
		if object.timer > 0
			object.timer -= 4
		else
#platform: USE_SCARLET
			object.state = SCARLETLOGO_WAIT
#endplatform
#platform: USE_DECOMP
			object.state = SCARLETLOGO_RETROLOCKOUT
#endplatform
		end if

		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break

	case SCARLETLOGO_WAIT
		object.timer++
		if object.timer == 180
			object.timer = 0
			object.state = SCARLETLOGO_FADETOBLACK
		end if
		break

	case SCARLETLOGO_FADETOBLACK
		if object.timer < 256
			object.timer += 4
			SetScreenFade(0x00, 0x00, 0x00, object.timer)
		else
			object.timer = 0
			object.state = SCARLETLOGO_INITIALISE
			ResetObjectEntity(object.entityPos, TypeName[ST Screen], 0, object.xpos, object.ypos)
			SetScreenFade(0x00, 0x00, 0x00, 0xFF)
		end if
		break

	case SCARLETLOGO_RETROLOCKOUT
		// Almost a dummy state
		// There's not really anything that can be done in here
		// Other than existing with those 3 lines of comment...
		break
		
	end switch

#platform: USE_SCARLET
	// Add some checks if the player wants to skip the screen already
	// This wasn't present in the code when Past Geared first coded it (as if it wasn't already obvious LOL)
	CheckTouchRect(0, 0, screen.xsize, screen.ysize)
	if checkResult > -1
		object.state = SCARLETLOGO_FADETOBLACK
	end if

	if keyPress[0].start == true
		object.state = SCARLETLOGO_FADETOBLACK
	end if

	if keyPress[0].buttonA == true
		object.state = SCARLETLOGO_FADETOBLACK
	end if
#endplatform
end event


event ObjectDraw
	temp0 = screen.xcenter

	if screen.xsize <= 424
		temp0 -= 37
	else
		if screen.xsize >= 425
			temp0 -= 35
		end if
	end if

	DrawSpriteScreenXY(0, temp0, screen.ycenter)
end event


event ObjectStartup
#platform: USE_DECOMP
    LoadSpriteSheet("Title/ErrorScreenV4.gif")
#endplatform
#platform: USE_SCARLET
    LoadSpriteSheet("Title/RetroEngine.gif")
#endplatform

	// Past Geared used v3's foreach method when he first coded this
	// Now me (Future Geared) & Klash (Klashicola) have used the right type of foreach (in case, v4's foreach method) :D
	foreach (0, arrayPos0, ALL_ENTITIES) // Normally i'd put a TypeName[Scarlet Engine Logo] (or smth like that) instead of the 0 in there, but considering the possibility of it not being a type in the scene, we have to work-around with it =/
		object[arrayPos0].priority = PRIORITY_ACTIVE
		object[arrayPos0].ixpos = 160
		object[arrayPos0].iypos = 120
	next

	// Used to be above the foreach, moved down to here for accuracy reasons
	// This goes for both the spritesheets btw
	SpriteFrame(-215, -120, 624, 240, 0, 0) // #0 - The Logo itself
	
	// Hide touch controls
	options.touchControls = false
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
    LoadSpriteSheet("Title/RetroEngine.gif")
	SpriteFrame(-215, -120, 624, 240, 0, 0)

	// This object is set by the scene not by the editor so
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
