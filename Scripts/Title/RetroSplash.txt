// ----------------------------------
// RSDK Project: Virtua Scarlet
// Script Description: Splash/Lock-out Screen Object
// Script Author: Klashicola/Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer // Past Geared used to call this "object.whatever" when it was clearly a timer :sob:

// States
private alias 0 : SCARLETLOGO_INITIALISE
private alias 1 : SCARLETLOGO_FADEFROMBLACK
private alias 2 : SCARLETLOGO_WAIT
private alias 3 : SCARLETLOGO_FADETOBLACK

// Priority
public alias 1 : PRIORITY_ACTIVE


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case SCARLETLOGO_INITIALISE
		LoadPalette("RetroLogo.act", 0, 0, 0, 256)
		LoadVideo("disclaimer")
		object.state = SCARLETLOGO_FADEFROMBLACK
		object.timer = 256
		SetScreenFade(0x00, 0x00, 0x00, 0xFF)
		break

	case SCARLETLOGO_FADEFROMBLACK
		if object.timer > 0
			object.timer -= 4
		else
			object.state = SCARLETLOGO_WAIT
		end if

		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break

	case SCARLETLOGO_WAIT
#platform: USE_SCARLET
		object.timer++
		if object.timer == 180
			object.timer = 0
			object.state = SCARLETLOGO_FADETOBLACK
		end if
#endplatform
		break

	case SCARLETLOGO_FADETOBLACK
		if object.timer < 256
			object.timer += 4
			SetScreenFade(0x00, 0x00, 0x00, object.timer)
		else
			object.timer = 0
			object.state = SCARLETLOGO_INITIALISE
			LoadVideo("logoREFG")
			ResetObjectEntity(object.entityPos, TypeName[ST Screen], 0, object.xpos, object.ypos)
			LoadPalette("VSS.act", 0, 0, 0, 256)
			SetScreenFade(0x00, 0x00, 0x00, 0xFF)
		end if
		break
		
	end switch

#platform: USE_SCARLET
	// Add some checks if the player wants to skip the screen already
	// This wasn't present in the original code of when Past Geared first coded it (as if it wasn't already obvious LOL)
	CheckTouchRect(0, 0, screen.xsize, screen.ysize)
	if checkResult > -1
		object.state = SCARLETLOGO_FADETOBLACK
	end if

	if keyPress[0].start == true
		object.state = SCARLETLOGO_FADETOBLACK
	end if

	if keyPress[0].buttonA == true
		object.state = SCARLETLOGO_FADETOBLACK
	end if
#endplatform
end event


event ObjectDraw
	temp0 = screen.xcenter

	if screen.xsize <= 424
		temp0 -= 37
	else
		if screen.xsize >= 425
			temp0 -= 35
		end if
	end if

	DrawSpriteScreenXY(0, temp0, screen.ycenter)
end event


event ObjectStartup
    LoadSpriteSheet("Title/ErrorScreenV4.gif")
#platform: USE_SCARLET
    LoadSpriteSheet("Title/RetroEngine.gif")
#endplatform
	SpriteFrame(-215, -120, 624, 240, 0, 0) // #0 - Scarlet Engine's Splash Screen

	// Past Geared used v3's foreach method when he first coded this
	// Now me (Future Geared) & Klash (Klashicola) have used the right type of foreach (in case, v4's foreach method) :D
	foreach (0, arrayPos0, ALL_ENTITIES) // Normally there would be a TypeName[Splash Lockout Screen] instead of the 0 in there, but considering the possibility of it not being a type in the scene config, we have to work-around with it like this =/
		object[arrayPos0].priority = PRIORITY_ACTIVE
		object[arrayPos0].ixpos = 160
		object[arrayPos0].iypos = 120
	next
	
	// Hide touch controls
	options.touchControls = false
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
    LoadSpriteSheet("Title/RetroEngine.gif")
	SpriteFrame(-215, -120, 624, 240, 0, 0)

	// This object is set by the scene not by the editor so
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
