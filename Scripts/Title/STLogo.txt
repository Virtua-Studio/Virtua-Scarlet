// ----------------------------------
// RSDK Project: Virtua Scarlet
// Script Description: Logo Object
// Script Author: Klashicola/Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
// value1 & value2 are unused
private alias object.value3 : object.levelSelectCheatPos
private alias object.value4 : object.regionSwitchCheatPos

// States
private alias 0  : LOGO_SETUP
private alias 1  : LOGO_FADEIN
private alias 2  : LOGO_STARTMUSIC
private alias 3  : LOGO_SHOWSONICTAILS
private alias 4  : LOGO_SHOWBG
private alias 5  : LOGO_FINISHEDANIMATING

// Soundtrack
private alias 0  : OST_CURRENT


// ========================
// Events
// ========================

event ObjectUpdate
	LoadPalette("title.act", 0, 0, 0, 256)
	
	switch object.state
	case LOGO_SETUP
		if object.timer < 10
			object.timer++
		else
			object.timer = 0
			object.alpha = 0x100
			object.state++
		end if
		break

	case LOGO_FADEIN
		if object.alpha > 0
			object.alpha -= 12
		else
			object.state++
		end if
		break

	case LOGO_STARTMUSIC
		if object.timer < 32
			object.timer++
		else
			object.timer = 0
			PlayMusic(0)
			object.state++
		end if
		break

	case LOGO_SHOWSONICTAILS
		if object.timer < 40
			object.timer++
		else
			object.timer = 0
			object.alpha = 256
			stage.activeLayer[0] = 2 // Show Title BG
			object.state++
		end if
		break

	case LOGO_SHOWBG
		if object.alpha > 0
			object.alpha -= 12
		else
			object.state++

			object[+1].type = TypeName[Start Message]
			object[+1].priority = PRIORITY_ACTIVE
			object[+1].xpos = screen.xcenter
			object[+1].ypos = 208
		end if
		break

	case LOGO_FINISHEDANIMATING
		object.timer++
		if object.timer > 7
			tileLayer[4].deformationOffset++
			object.timer = 0
		end if
		break
		
	end switch

	temp0 = screen.xcenter
	FlipSign(temp0)
	temp0 += 256
	screen.xoffset = temp0
end event


event ObjectDraw
	switch object.state
	case LOGO_SETUP
		ClearScreen(1)
		break

	case LOGO_FADEIN
		ClearScreen(1)

		DrawRect(0, 0, screen.xsize, screen.ysize, 0x00, 0x00, 0x00, object.alpha)
		break

	case LOGO_SHOWEMBLEM
		ClearScreen(1)
		break

	case LOGO_SHOWSONICTAILS
		ClearScreen(1)
		break

	case LOGO_SHOWBG
		DrawRect(0, 0, screen.xsize, screen.ysize, 0xE0, 0xE0, 0xE0, object.alpha)

		// Draw Logo
		DrawSprite(0)

		// Draw Copyright
		temp0 = screen.xsize
		temp0 -= 96
		DrawSpriteScreenXY(1, temp0, 216)
		break

	case LOGO_FINISHEDANIMATING
		// Draw Logo
		DrawSprite(0)

		// Draw Copyright
		temp0 = screen.xsize
		temp0 -= 96
		DrawSpriteScreenXY(1, temp0, 216)
		break
		
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Title/Title.gif")
	Options.Soundtrack = saveRAM[38]
	if Options.Soundtrack == OST_CURRENT
		SetMusicTrack("titlescreen.ogg", 0, false)
	else
		SetMusicTrack("TitlescreenSAGE.ogg", 0, false)
	end if

	arrayPos0 = 0x100
	while arrayPos0 < 0x240
		arrayPos1 = arrayPos0
		arrayPos1 -= 0x100
		Rand(stage.deformationData2[arrayPos1], 4)
		temp0 = arrayPos0
		temp0 &= 1
		if temp0 == 1
			FlipSign(stage.deformationData2[arrayPos1])
		end if
		stage.deformationData2[arrayPos0] = stage.deformationData2[arrayPos1]
		arrayPos0++
	loop

    SpriteFrame(-103, -48, 290, 89, 218, 1)	// Logo 			- #0
	SpriteFrame(-72, 0, 160, 16, 1, 79)			// Copyright 			- #1
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Title/Title.gif")
	SpriteFrame(-103, -48, 290, 89, 218, 1)	// Virtua Scarlet Logo 			- #0
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
