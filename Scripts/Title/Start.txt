// ----------------------------------
// RSDK Project: Virtua Scarlet
// Script Description: Start Message Object
// Script Author: Klashicola/Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
private alias object.value1 : object.attractTimer

// States
private alias  0 : START_AWAITACTION
private alias  1 : START_GOTO_MENU

// Presentation Stages
private alias 0 : STAGE_P_TITLE
private alias 3 : STAGE_P_MENU


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case START_AWAITACTION
		CheckTouchRect(0, 0, screen.xsize, screen.ysize)
		if keyPress[0].start == true
			checkResult = true
		end if

		if keyPress[0].buttonA == true
			checkResult = true
		end if

		if keyPress[0].buttonB == true
			checkResult = true
		end if

		if keyPress[0].buttonC == true
			checkResult = true
		end if

		if checkResult > -1
			object.state = START_GOTO_MENU

			PlaySfx(SfxName[Menu Select], false)
			StopMusic()
		else
			object.attractTimer++
			if object.attractTimer == 1620
				LoadVideo("Opening")
			end if
		end if
		break

	case START_GOTO_MENU
		object.timer += 8
		if object.timer == 320
			LoadVideo("Opening") // Yeah, it is hers too for a reason, if you didn't know
			stage.listPos = STAGE_P_MENU
			LoadStage()
		end if
		SetScreenFade(0x00, 0x00, 0x00, object.timer)
		break

	end switch
end event


event ObjectDraw
	object.animationTimer++
	if object.animationTimer > 59
		object.animationTimer = 0
	end if

	if object.animationTimer > 29
		DrawSpriteScreenXY(0, screen.xcenter, object.ypos)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Title/Title.gif")
	switch engine.deviceType
	case STANDARD
		SpriteFrame(-72, -23, 144, 8, 1, 47)		// Press Start	- #0
		break
			
		case MOBILE
		SpriteFrame(-57, -23, 112, 8, 1, 67)	// Touch To Start 	- #0
		break
		
	end switch
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Title/Title.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)	// Press Button 	- #0
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
