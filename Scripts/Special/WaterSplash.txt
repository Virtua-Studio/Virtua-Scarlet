// ----------------------------------
// RSDK Project: Virtua Scarlet
// Script Description: Water Splash Object
// Script Author: Virtua Team
// ----------------------------------

// ========================
// Aliases
// ========================

// HUD Alias
private alias object.value7 : hud.waterTimer

// Player Alises
private alias object.xpos : ssSonic.xpos
private alias object.ypos : ssSonic.ypos
private alias object.value0 : ssSonic.zpos

// Tile Info
#alias 6 : TILEINFO_ANGLEA


// ========================
// Events
// ========================

event ObjectDraw
	if ssSonic.ypos > 0
		// If Sonic's in the air, he certainly isn't splashing around anymore so unload
		ResetObjectEntity(3, TypeName[Blank Object], 0, 0, 0)
	else
		// Find the player's truncated X and Z pos
		temp0 = ssSonic.xpos
		temp0 >>= 16
		
		temp1 = ssSonic[-1].zpos
		temp1 >>= 16
		
		// Get the tile type Sonic's currently standing on
		Get16x16TileInfo(checkResult, temp0, temp1, TILEINFO_ANGLEA)
		
		// Is it a water tile?
		if checkResult == 2
			if object.frame == 0
				PlaySfx(SfxName[Water Splash], false)
				
				if stage.timeEnabled == true
					hud[4].waterTimer = 40
				end if
			end if
			
			// Animate the object
			object.frame++
			if object.frame == 40
				object.frame = 28
			end if
			
			// Get the frame it should draw
			temp0 = object.frame
			temp0 >>= 2
			
			// Draw the sprite around the center of the screen
			DrawSpriteScreenXY(temp0, screen.xcenter, 224)
		else
			// Sonic's not on a water tile, erase the Water Splash object
			ResetObjectEntity(3, TypeName[Blank Object], 0, 0, 0)
			hud[4].waterTimer = 0
		end if
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Special/Objects.gif")
	
	// Water Splash Frames
	SpriteFrame(-20, -32, 40, 32, 1, 84)
	SpriteFrame(-20, -32, 40, 32, 42, 84)
	SpriteFrame(-20, -32, 40, 32, 83, 84)
	SpriteFrame(-20, -48, 40, 48, 124, 58)
	SpriteFrame(-20, -64, 40, 64, 165, 58)
	SpriteFrame(-20, -8, 40, 8, 124, 107)
	SpriteFrame(-20, -8, 40, 8, 124, 116)
	SpriteFrame(-20, -32, 40, 32, 42, 84)
	SpriteFrame(-20, -32, 40, 32, 83, 84)
	SpriteFrame(-20, -32, 40, 32, 1, 84)
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Special/Objects.gif")
	SpriteFrame(-20, -32, 40, 32, 42, 84)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
