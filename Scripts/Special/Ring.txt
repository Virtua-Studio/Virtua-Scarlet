// ----------------------------------
// RSDK Project: Virtua Scarlet
// Script Description: Ring Object
// Script Author: Virtua Team
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.zpos
private alias object.value1 : object.zvel
private alias object.value2 : object.timer
private alias object.value3 : object.screenDepth
private alias object.value4 : object.screenXPos


// ========================
// Events
// ========================

event ObjectUpdate
	// Update movements
	object.xpos += object.xvel
	object.ypos += object.yvel
	object.zpos += object.zvel

	// Apply a gravity of an eight of a pixel per frame
	object.yvel -= 0x2000

	if object.ypos < 0
		// If the ring has hit the floor, then bounce back up
		object.ypos = 0
		FlipSign(object.yvel)
	end if

	temp0 = object.xpos
	temp0 -= tileLayer[0].xpos
	temp0 >>= 8

	temp1 = object.zpos
	temp1 -= tileLayer[0].zpos
	temp1 >>= 8

	Sin(temp2, tileLayer[0].angle)
	temp2 *= temp1
	Cos(temp3, tileLayer[0].angle)
	temp3 *= temp0

	object.screenXPos = temp2
	object.screenXPos -= temp3
	object.screenXPos >>= 9

	Cos(temp2, tileLayer[0].angle)
	temp2 *= Temp1
	Sin(temp3, tileLayer[0].angle)
	temp3 *= temp0

	object.screenDepth = temp2
	object.screenDepth += temp3
	object.screenDepth >>= 9

	// Make the ring despawn after a little bit
	object.timer++
	if object.timer == 160
		object.type = TypeName[Blank Object]
	end if
end event


event ObjectDraw
	if object.screenDepth > 256 // Don't draw if the Ring is too close to the screen
		// Do some maths to find the position & scale the Ring should be at
		object.scale = 0xC00000
		object.scale /= object.screenDepth
		
		temp0 = 768
		temp0 -= object.scale
		temp0 /= 6
		
		temp1 = object.screenDepth
		temp1 >>= 12
		
		if temp0 > 0
			object.scale += temp0
			object.scale -= temp1
		end if
		
		temp0 = object.screenXPos
		temp0 *= 192
		temp0 /= object.screenDepth
		temp0 += screen.xcenter
		
		temp1 = tileLayer[0].ypos
		temp1 -= object.ypos
		temp1 >>= 8
		temp1 *= 96
		temp1 /= object.screenDepth
		temp1 += 128
		
		// Find the sprite to draw the Ring with
		temp2 = object.timer
		temp2 &= 31
		temp2 >>= 3
		
		DrawSpriteScreenFX(temp2, FX_SCALE, temp0, temp1)
		
		// Find the position of the ground where the ring is
		temp1 = tileLayer[0].ypos
		temp1 >>= 8
		temp1 *= 96
		temp1 /= object.screenDepth
		temp1 += 128
		
		// Draw a shadow there
		DrawSpriteScreenFX(4, FX_SCALE, temp0, temp1)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("Special/Objects.gif")
	
	// Ring Frames
	SpriteFrame(-10, -24, 20, 24, 52, 59)
	SpriteFrame(-7, -24, 15, 24, 73, 59)
	SpriteFrame(-4, -24, 9, 24, 89, 59)
	SpriteFrame(-7, -24, 15, 24, 99, 59)
	
	// Shadow Frame
	SpriteFrame(-9, -4, 18, 8, 83, 117)
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Special/Objects.gif")
	SpriteFrame(-10, -24, 20, 24, 52, 59)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
